#VARIABLE shadeWidth {5};
#VARIABLE shadeHeight {5};

#VARIABLE macromode {0};

#nop Assume we're starting in the heart;
#VARIABLE currentNode {12};

#VARIABLE shadesIndicator {};

#VARIABLE macroStatus {On};

#function {translateDir}
{
    #var result {};
    #script {result} {python3 src/shades/shades.py $currentNode %0};

    #var direction {};
    #var newNode {};

    #list {result} {get} {1} {direction};
    #list {result} {get} {2} {newNode};

    #if {$direction}
    {
        #if {$newNode}
        {
            #nop #showme %0;
            #nop #showme Prev Node: [$currentNode];
            #nop #showme Curr Node: [$newNode];

            #format {shadesIndicator} {%c%0%c} {<fcf>} {<098>};
            #showme $shadesIndicator;

            #format {shadesIndicator} {%cPrev Node: [$currentNode]%c} {<060>} {<098>};
            #showme $shadesIndicator;

            #format {shadesIndicator} {%cCurr Node: [$newNode]%c} {<020>} {<098>};
            #showme $shadesIndicator;

            #var currentNode {$newNode};
            #return $direction;
        }
    };
    #else
    {
        #showme Failed to move! Toggle macro with '5'!;
    }
}

#function {togglemacros} 
{
    #if {$macromode == 1}
    {
        #nop Directions;
        #unmacro {i};
        #unmacro {l};
        #unmacro {j};
        #unmacro {,};
        #unmacro {o};
        #unmacro {u};
        #unmacro {m};
        #unmacro {.};

        #nop Commands;
        #unmacro {k};
        #unmacro {d};
        #unmacro {f};
        #unmacro {e};
        #unmacro {r};
        #var macromode {0};
    };
    #else
    {
        #VARIABLE currentNode {12};
        #format {shadesIndicator} {%cAssuming shade heart [Node $currentNode]%c} {<020>} {<098>};
        #showme $shadesIndicator;

        #nop Directions;
        #macro {i} 
        {
            @translateDir{n};
        };
        #macro {l} 
        {
            @translateDir{e};
        };
        #macro {j} 
        {
            @translateDir{w};
        };
        #macro {,} 
        {
            @translateDir{s};
        };
        #macro {o} 
        {
            @translateDir{ne};
        };
        #macro {u} 
        {
            @translateDir{nw};
        };
        #macro {m} 
        {
            @translateDir{sw};
        };
        #macro {.} 
        {
            @translateDir{se};
        };


        #nop Commands;
        #macro {k} 
        {
            #send ka;
        };
        #macro {d} 
        {
            #send lose all;
            #send unfollow all;
        };
        #macro {f} 
        {
            #send group follow all;
        };
        #macro {e} 
        {
            #send put dirks in bag;
        };
        #macro {r} 
        {
            #send get dirks from bag;
            #send hold dirks;
        };
        #var macromode {1};
    }
}

#alias {/sh} 
{
    #if {$macromode}
    {
        #VARIABLE macroStatus {Off};
    };
    #else
    {
        #VARIABLE macroStatus {On};
    }

    #format {shadesIndicator} {%cShade Mapper: $macroStatus%c} {<060>} {<098>};
    #showme $shadesIndicator;
    @togglemacros{};
};